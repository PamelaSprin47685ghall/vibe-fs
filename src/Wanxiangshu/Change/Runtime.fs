namespace Wanxiangshu.Change

open Wanxiangshu.Git
open Wanxiangshu.Interaction.Authority
open Wanxiangshu.Interaction.Dispatch
open Wanxiangshu.Mission.Finality
open Wanxiangshu.Mission.Manager
open Wanxiangshu.Mission.Manager.Life
open Wanxiangshu.Mission.Review
open Wanxiangshu.Mission.Review.Barrier
open Wanxiangshu.Mission.Review.Judgement
open Wanxiangshu.Mission.WorkRecord
open Wanxiangshu.Strength.Persistence
open Wanxiangshu.Strength.Replica

open System.Collections.Generic
open System.IO
open System.Threading.Tasks
open FsToolkit.ErrorHandling
open Wanxiangshu.Change
open Wanxiangshu.Composition.Durable
open Wanxiangshu.Persistence.Journal
open Wanxiangshu.Foundation
open Wanxiangshu.Foundation.Identity
open Wanxiangshu.Context.Companion
open Wanxiangshu.Context.Companion.Blogger.Runtime
open Wanxiangshu.Enforcer
open Wanxiangshu.Enforcer.Cycle
open Wanxiangshu.Enforcer.Guidance
open Wanxiangshu.Execution.Delegation.Fork
open Wanxiangshu.Execution.Delegation.Handle
open Wanxiangshu.Execution.Delegation.SyncDelegate
open Wanxiangshu.Execution.Fission
open Wanxiangshu.Execution.Session
open Wanxiangshu.Execution.Session.Attachment
open Wanxiangshu.Execution.Session.Recovery
open Wanxiangshu.Execution.Session.Wait
open Wanxiangshu.Participant.Persona
open Wanxiangshu.Participant.Provider
open Wanxiangshu.Participant.Provider.Attempt.Fallback
open Wanxiangshu.Strength

module private OrchestratorRuntimeDecisions =
    /// Durable terminal fact closes the job permanently (GLORY-068).
    let requireActiveJob (record: ManagerJobProjection) : Result<ManagerJobProjection, string> =
        match record.Terminal with
        | Some _ -> Error(sprintf "Manager job is no longer active: %s" (ManagerJobId.value record.ManagerJobId))
        | None -> Ok record

    let mapTaskError (mapper: 'error -> 'mapped) (operation: Task<Result<'value, 'error>>) =
        task {
            let! result = operation
            return Result.mapError mapper result
        }

    let releaseWorktreeOnError (worktree: WorktreeResource) (operation: Task<Result<'value, 'error>>) =
        task {
            match! operation with
            | Ok value -> return Ok value
            | Error error ->
                let! _ = worktree.Release()
                return Error error
        }

    /// `AdoptCreated` re-enters an admission whose `WorktreeCreated` is already
    /// durable, so it owes no second fact. Every other reconciliation still owes
    /// the fact for the physical worktree it just acquired.
    let pendingWorktreeCreatedFact (decision: WorktreeReconciliationDecision) fact =
        match decision with
        | WorktreeReconciliationDecision.AdoptCreated -> None
        | _ -> Some fact

    /// A journaled admission has durable worktree evidence, so a failed manager
    /// start leaves the physical worktree for recovery. Without a journal nothing
    /// records the worktree, so failure must release it.
    let releaseWorktreeUnlessJournaled journalPort worktree operation =
        journalPort
        |> Option.map (fun _ -> operation)
        |> Option.defaultWith (fun () -> operation |> releaseWorktreeOnError worktree)

/// Runtime owner for ManagerJob resources. Every job runs the sequential
/// direct-CE OrchestratorProgram workflow; the mailbox contains only final post-FF verdicts.
///
/// ORCH-006's `ManagerJobCreated` is the only writer here, and it is written after
/// the Manager fork returns a `SessionId` but before the program awaits it. That
/// ordering is what makes a crash recoverable in either direction: no fact means no
/// job, and a fact means a live Manager whose session is known.
type Orchestrator
    (
        git: GitPort,
        manager: ManagerPort,
        repoPath: string,
        targetRef: TargetRef,
        ?journal: OrchestratorJournalPort,
        ?lockRepoPath: string
    ) =

    let mailbox = VerdictMailbox()
    let journalPort = journal

    let gatePath =
        IntegrationGate.lockPath (defaultArg lockRepoPath repoPath) (TargetRef.value targetRef)

    let appendFact stream fact =
        taskResult {
            match journalPort with
            | None -> return ()
            | Some port ->
                let! _ = port.AppendFact stream fact
                return ()
        }

    let snapshot () =
        journalPort
        |> Option.map (fun port -> port.Snapshot())
        |> Option.defaultValue Fold.empty

    let acquirePublishGate () =
        task {
            let! gate = IntegrationGate.acquire gatePath
            return { Release = fun () -> gate.Release() }
        }

    let programDeps: OrchestratorProgramDeps =
        { Git = git
          Manager = manager
          AppendFact = appendFact
          Snapshot = snapshot
          AcquirePublishGate = acquirePublishGate }

    let startPublication (job: ManagerJob) =
        mailbox.StartJob()

        task {
            let! verdict = OrchestratorProgram.run programDeps job
            mailbox.Publish verdict
        }
        |> ignore

    let defaultWorktreePath (jobId: ManagerJobId) =
        WorktreePath.create (Path.Combine(Path.GetTempPath(), sprintf "wanxiangshu-%s" (ManagerJobId.value jobId)))

    /// ORCH-004: a new ManagerJob.
    ///
    /// `managerAgent` is required and never defaulted. ORCH-003 keeps one Manager per
    /// job for its whole life, and PROMPT-008 forbids rebuilding a managed name from
    /// a role — a `deep-manager` job resumed as `fast-manager` would carry the wrong
    /// FALLBACK-002 A/B pair for the rest of the run.
    let forkManagerCore
        (jobId: ManagerJobId)
        (managerAgent: string)
        (byname: string)
        (prompt: string)
        (expectedToolCalls: int option)
        (worktreePath: WorktreePath option)
        : Task<Result<OrchestratorHandle, OrchestratorVerdict>> =
        taskResult {
            let! dirty = git.IsDirty(WorktreePath.create repoPath) |> TaskResultCE.ofTask

            do!
                if dirty then
                    Error(OrchestratorVerdict.RejectedDirty "Worktree is dirty")
                else
                    Ok()

            let path = defaultArg worktreePath (defaultWorktreePath jobId)
            // PERSIST-009: effect identity is deterministic before git runs.
            let identity = WorktreeCommands.identityOf jobId

            let requestFact =
                OrchestratorFact.WorktreeCreateRequested
                    {| ManagerJobId = jobId
                       WorktreeIdentity = identity
                       WorktreePath = path |}

            let integration failurePrefix error =
                OrchestratorVerdict.IntegrationFailed(jobId, sprintf "%s: %s" failurePrefix error)

            let durableEffect =
                (snapshot ()).AgentProjections.Orchestrator
                |> OrchestratorProjection.tryWorktreeEffect identity

            let! observation =
                match durableEffect with
                | None ->
                    Task.FromResult WorktreeReconciliationObservation.NoDurableEffect
                    |> TaskResultCE.ofTask
                | Some(WorktreeEffectStatus.Created receipt) ->
                    Task.FromResult(
                        WorktreeReconciliationObservation.CreatedReceipt(receipt.ManagerJobId, receipt.WorktreePath)
                    )
                    |> TaskResultCE.ofTask
                | Some(WorktreeEffectStatus.Requested request) when
                    request.ManagerJobId <> jobId || request.WorktreePath <> path
                    ->
                    Task.FromResult(
                        WorktreeReconciliationObservation.RequestedConflict(request.ManagerJobId, request.WorktreePath)
                    )
                    |> TaskResultCE.ofTask
                | Some(WorktreeEffectStatus.Requested request) ->
                    task {
                        let! physical = git.ListWorktrees()

                        return
                            WorktreeReconciliationObservation.RequestedAmbiguity(
                                request.ManagerJobId,
                                request.WorktreePath,
                                physical
                            )
                    }
                    |> TaskResultCE.ofTask

            let decision =
                OrchestratorProjection.decideWorktreeReconciliation jobId identity path observation

            let reconciliationFailure failure =
                match failure with
                | WorktreeReconciliationFailure.DurableOwnershipConflict ->
                    "Durable worktree intent conflicts with the requested job or path"
                | WorktreeReconciliationFailure.WorktreeQueryFailed error ->
                    sprintf "Failed to query requested worktree ambiguity: %s" error
                | WorktreeReconciliationFailure.PhysicalIdentityPathConflict ->
                    "Physical worktree identity/path evidence conflicts with the durable request"

            do!
                match decision with
                | WorktreeReconciliationDecision.RequestThenCreate ->
                    appendFact StreamId.Workspace requestFact
                    |> OrchestratorRuntimeDecisions.mapTaskError (integration "Failed to persist worktree request")
                | WorktreeReconciliationDecision.Reject failure ->
                    Error(integration "Worktree reconciliation rejected" (reconciliationFailure failure))
                    |> Task.FromResult
                | _ -> Ok() |> Task.FromResult

            let acquireWorktree () =
                match decision with
                | WorktreeReconciliationDecision.RequestThenCreate
                | WorktreeReconciliationDecision.CreateAfterProvenMissing ->
                    WorktreeResource.Create(git, jobId, path)
                    |> OrchestratorRuntimeDecisions.mapTaskError (integration "Failed to create worktree")
                | WorktreeReconciliationDecision.AdoptThenRecordCreated
                | WorktreeReconciliationDecision.AdoptCreated ->
                    Ok(WorktreeResource.Adopt(git, identity, path)) |> Task.FromResult
                | WorktreeReconciliationDecision.Reject failure ->
                    Error(integration "Worktree reconciliation rejected" (reconciliationFailure failure))
                    |> Task.FromResult

            let persistWorktreeCreated createdFact =
                appendFact StreamId.Workspace createdFact
                |> OrchestratorRuntimeDecisions.mapTaskError (integration "Failed to persist worktree created")

            let recordWorktreeCreated (worktree: WorktreeResource) =
                OrchestratorFact.WorktreeCreated
                    {| ManagerJobId = jobId
                       WorktreeIdentity = worktree.Identity
                       WorktreePath = path |}
                |> OrchestratorRuntimeDecisions.pendingWorktreeCreatedFact decision
                |> Option.map persistWorktreeCreated
                |> Option.defaultWith (fun () -> Ok() |> Task.FromResult)
                |> OrchestratorRuntimeDecisions.releaseWorktreeOnError worktree

            let startManager (worktree: WorktreeResource) =
                taskResult {
                    // The Manager session is created before the job fact because
                    // ORCH-006 persists its SessionId. Its first prompt remains
                    // deferred until ManagerJobCreated is durable.
                    let! managerSessionId =
                        manager.StartManager
                            { JobId = jobId
                              ManagerAgent = managerAgent
                              Worktree = path
                              Prompt = prompt
                              ExpectedToolCalls = expectedToolCalls }
                        |> OrchestratorRuntimeDecisions.mapTaskError (integration "Failed to start manager")

                    let fact =
                        OrchestratorFact.ManagerJobCreated
                            {| ManagerJobId = jobId
                               ManagerSessionId = managerSessionId
                               ManagerAgent = managerAgent
                               Byname = byname
                               WorktreeIdentity = worktree.Identity
                               WorktreePath = path
                               TargetRef = targetRef
                               TargetBranchFrozen = TargetRef.value targetRef |}

                    do!
                        appendFact StreamId.Workspace fact
                        |> OrchestratorRuntimeDecisions.mapTaskError (integration "Failed to persist manager job")

                    let job =
                        { JobId = jobId
                          ManagerSessionId = managerSessionId
                          ManagerAgent = managerAgent
                          TargetRef = targetRef
                          Worktree = worktree }

                    return job
                }

            return!
                task {
                    let! admissionGate = IntegrationGate.acquire gatePath

                    let! outcome =
                        task {
                            try
                                return!
                                    taskResult {
                                        let! worktree = acquireWorktree ()
                                        do! recordWorktreeCreated worktree
                                        journalPort |> Option.iter (fun _ -> worktree.MarkDurable())

                                        return!
                                            startManager worktree
                                            |> OrchestratorRuntimeDecisions.releaseWorktreeUnlessJournaled
                                                journalPort
                                                worktree
                                    }
                            with ex ->
                                return Error(integration "Manager admission failed" ex.Message)
                        }

                    do! admissionGate.Release()

                    return!
                        taskResult {
                            let! job = outcome

                            do!
                                manager.SendManagerPrompt jobId
                                |> OrchestratorRuntimeDecisions.mapTaskError (
                                    integration "Failed to send manager prompt"
                                )

                            startPublication job
                            return job.Handle
                        }
                }

        }

    member _.ForkManager
        (
            jobId: ManagerJobId,
            managerAgent: string,
            prompt: string,
            ?worktreePath: WorktreePath,
            ?byname: string,
            ?expectedToolCalls: int
        ) =
        let providerByname =
            match byname with
            | Some value when not (System.String.IsNullOrWhiteSpace value) -> value.Trim()
            | _ -> managerAgent

        forkManagerCore jobId managerAgent providerByname prompt expectedToolCalls worktreePath

    /// ORCH-007: resume a persisted job. The worktree is adopted by its durable
    /// identity, never recreated, and the Manager is the one the fact names.
    member _.RecoverManagerJob(record: ManagerJobProjection) =
        let worktree =
            WorktreeResource.Adopt(git, record.WorktreeIdentity, record.WorktreePath)

        startPublication
            { JobId = record.ManagerJobId
              ManagerSessionId = record.ManagerSessionId
              ManagerAgent = record.ManagerAgent
              TargetRef = record.TargetRef
              Worktree = worktree }

    /// GLORY-068: reuse an active ManagerJob — the SAME worktree and the SAME
    /// Manager session continue with an appended requirement ("十年修得同船渡").
    /// Refuses terminal jobs; a finished job never revives.
    member this.ContinueManager(jobId: ManagerJobId, prompt: string) : Task<Result<WorktreePath, string>> =
        taskResult {
            let projection = snapshot ()

            let! record =
                OrchestratorProjection.tryFind jobId projection.AgentProjections.Orchestrator
                |> Result.requireSome (sprintf "Unknown manager job: %s" (ManagerJobId.value jobId))
                |> Result.bind OrchestratorRuntimeDecisions.requireActiveJob

            do! manager.ResumeManager record.ManagerJobId record.WorktreePath prompt
            return record.WorktreePath
        }

    /// EXEC-019: bounded FIFO batch with local interrupt (≠ lifecycle Cancel).
    member _.JoinPublishedBatch(maxCount: int, interrupt: Task<JoinInterruptReason>) =
        mailbox.JoinAvailable(maxCount, interrupt)
