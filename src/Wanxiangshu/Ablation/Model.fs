namespace Wanxiangshu.Ablation

type AblationMode =
    | Ablated
    | Borrowed
    | Active

type AblationNodeId = AblationNodeId of string

type AblationAudit =
    { Profile: string option
      ManifestVersion: string
      ManifestFingerprint: string
      NodeCount: int }

type AblationRegistry =
    { Modes: Map<AblationNodeId, AblationMode>
      Audit: AblationAudit }

type AblationLoadError =
    | MissingManifest of path: string
    | InvalidManifest of reason: string
    | UnknownProfile of name: string
    | InvalidMode of node: string * raw: string
    | DagViolation of reason: string

type ManifestNode =
    { Id: string
      Package: string
      Station: int
      Kind: string
      Parent: string option }

type ManifestEdge =
    { From: string
      To: string
      Kind: string }

type ManifestDocument =
    { Version: string
      Nodes: ManifestNode list
      Edges: ManifestEdge list }

type ProfilesDocument = { Profiles: Map<string, Map<string, string>> }

type ToolMapDocument = { Tools: Map<string, string> }

type FactMapDocument = { Facts: Map<string, string> }

[<RequireQualifiedAccess>]
module AblationNodeId =
    let create (raw: string) = AblationNodeId raw
    let value (AblationNodeId raw) = raw

[<RequireQualifiedAccess>]
module AblationMode =
    let parse (raw: string) =
        match raw.Trim().ToLowerInvariant() with
        | "ablated" -> Some AblationMode.Ablated
        | "borrowed" -> Some AblationMode.Borrowed
        | "active" -> Some AblationMode.Active
        | _ -> None

    let wire mode =
        match mode with
        | AblationMode.Ablated -> "ablated"
        | AblationMode.Borrowed -> "borrowed"
        | AblationMode.Active -> "active"

    let allowsToolExecution mode =
        match mode with
        | AblationMode.Active -> true
        | AblationMode.Borrowed
        | AblationMode.Ablated -> false

    let allowsToolSchema mode =
        match mode with
        | AblationMode.Active
        | AblationMode.Borrowed -> true
        | AblationMode.Ablated -> false

[<RequireQualifiedAccess>]
module AblationRegistry =
    let modeFor (id: AblationNodeId) (registry: AblationRegistry) =
        registry.Modes
        |> Map.tryFind id
        |> Option.defaultValue AblationMode.Active

    let isActive id registry = modeFor id registry = AblationMode.Active

    let isBorrowedOrActive id registry =
        match modeFor id registry with
        | AblationMode.Active
        | AblationMode.Borrowed -> true
        | AblationMode.Ablated -> false
